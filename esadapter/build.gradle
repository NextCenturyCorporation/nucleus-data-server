apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.ncc.neon'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

springBoot {
    mainClassName = 'com.ncc.neon.NeonServerApplication'
}

//Build traditional jar which is what we want as the library
jar {
    enabled = true
    //Excluding this as its a only use if for VScode see class for more info
    exclude '**/EsNeonServerApplication*'
}

bootJar {
    enabled = true
    classifier = 'boot'
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    exclude '**/EsNeonServerApplication*'
    into("build/dependency")
}

dependencies {
    compile project(':common')
    compile 'org.elasticsearch:elasticsearch:6.4.3'
    compile 'org.elasticsearch.client:elasticsearch-rest-client:6.4.3'
    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.4.3'
    compileLibraries.each {
        compileOnly it
    }
    implementationLibraries.each {
        implementation it
    }
    runtimeLibraries.each {
        runtimeOnly it
    }
    testCompileLibraries.each {
        testCompile it
    }
    testImplementationLibraries.each {
        testImplementation it
    }
}
