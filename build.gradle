group = 'com.ncc.neon'

// Override default Elasticsearch version from Spring BOM to continue support of 6.4
ext['elasticsearch.version'] = '6.4.3';

buildscript {
    ext {
        springBootVersion = '2.2.1.RELEASE'
        compileLibraries = [
            'org.projectlombok:lombok',
            'io.projectreactor:reactor-core',
            'org.springframework.boot:spring-boot-starter:' + springBootVersion
        ]
        implementationLibraries = [
            'org.springframework.boot:spring-boot-starter-actuator:' + springBootVersion,
            'org.springframework.boot:spring-boot-starter-webflux:' + springBootVersion,
            'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
        ]
        runtimeLibraries = [
            'org.springframework.boot:spring-boot-devtools:' + springBootVersion
        ]
        testCompileLibraries = [
            'org.springframework.boot:spring-boot-starter-test:' + springBootVersion
        ]
        testImplementationLibraries = [
            'org.springframework.boot:spring-boot-starter-test:' + springBootVersion,
            'io.projectreactor:reactor-test'
        ]
    }
    repositories {
        mavenCentral()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
        maven {
          url "https://repo.spring.io/release"
        }
        maven {
          url "https://repo.spring.io/milestone"
        }
        maven {
          url "https://repo.spring.io/snapshot"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.22.0")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.0")
    }
}

apply plugin: 'com.palantir.docker'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

subprojects {
    version = '0.0.1-SNAPSHOT'
}

task dockerCopy(dependsOn: ['dockerPrepare', 'esadapter:unpack', 'sqladapter:unpack', 'server:unpack', 'common:unpack']) << {
    copy {
        from fileTree(".").include("*/build/dependency/BOOT-INF/lib/*").files
        into "build/docker/libs"
    }
    copy {
        from "server/build/dependency/META-INF"
        into "build/docker/meta"
    }
    copy {
        from "common/build/dependency/BOOT-INF/classes"
        into "build/docker/classes"
    }
    copy {
        from "server/build/dependency/BOOT-INF/classes"
        into "build/docker/classes"
    }
    copy {
        from "esadapter/build/dependency/BOOT-INF/classes"
        into "build/docker/classes"
    }
    copy {
        from "sqladapter/build/dependency/BOOT-INF/classes"
        into "build/docker/classes"
    }
}

docker {
    name "${project.group}/server"
}

docker.dependsOn(dockerCopy)
