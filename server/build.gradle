apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
apply plugin: 'com.gorylenko.gradle-git-properties'

group = 'com.ncc.neon'

repositories {
    mavenCentral()
    maven {
      url "https://repo.spring.io/release"
    }
    maven {
      url "https://repo.spring.io/milestone"
    }
    maven {
      url "https://repo.spring.io/snapshot"
    }
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

application {
    mainClassName = "com.ncc.neon.NeonServerApplication"
}

jar {
    enabled = true
}

bootJar {
    enabled = true
    classifier = 'boot'
}

dependencies {
    compile project(':common')
    serverAdapters.split(',').each {
        compile project(':' + it)
    }
    compileLibraries.each {
        compileOnly it
    }
    implementationLibraries.each {
        implementation it
    }
    runtimeLibraries.each {
        runtimeOnly it
    }
    testCompileLibraries.each {
        testCompile it
    }
    testImplementationLibraries.each {
        testImplementation it
    }
    implementation('com.opencsv:opencsv:4.6')
    implementation("com.squareup.okhttp3:okhttp:4.2.1")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

gitProperties {
    extProperty = 'gitProps'
}

generateGitProperties.outputs.upToDateWhen { false }

task saveBuildDateAndGitCommit(dependsOn: 'generateGitProperties') {
    doLast {
        new File(projectDir, "src/main/resources/autogenerate.properties").text = "Build Date = " + new Date() + 
            "\nGit Commit = " + project.ext.gitProps['git.commit.id'] + "\n"
    }
}

bootRun.dependsOn saveBuildDateAndGitCommit
bootRun.mustRunAfter saveBuildDateAndGitCommit
build.dependsOn saveBuildDateAndGitCommit
test.dependsOn saveBuildDateAndGitCommit
